
        {
            "cells": [
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["// can\u0027t yet format YamlFrontmatter ([\"title: Earnings Announcement Return (EAR)\"; \"category: Scripts\"; \"categoryindex: 2\"; \"index: 2\"], Some { StartLine = 2 StartColumn = 0 EndLine = 6 EndColumn = 8 }) to pynb markdown\n",
"\n",
"[![Script](img/badge-script.svg)](/ConferenceCalls//EarningsAnnouncementReturn.fsx)\u0026emsp;\n",
"[![Notebook](img/badge-notebook.svg)](/ConferenceCalls//EarningsAnnouncementReturn.ipynb)\n",
"\n",
"# Earnings Announcement Return (EAR)\n",
"\n",
"In the `TranscriptParsing.fsx` script, we downloaded earnings call transcripts, \n",
"ticker and exchange information, and even the exact date and time of each earnings call. \n",
"Ok great, now what ?\n",
"\n",
"In finance, a growing body of literature is concerned with applying \n",
"state of the art *text-mining* techniques on textual data with the objective \n",
"of conducting *sentiment analysis*. Such analysis is often conducted using \n",
"statistical learning methods such as *supervised* and *unsupervised* learning. \n",
"The key difference between these sets of methods/algorithms lies within their purpose. \n",
"While supervised learning is used for solving the task of *prediction*, \n",
"unsupervised learning is used for other tasks such as *data inference*. \n",
"Additionally, as the names suggest, while supervised learning algorithms \n",
"*learn* by working with *labeled datasets*, unsupervised learning \n",
"algorithms do not. For this very reason, it is often the case that, when compared \n",
"to unsupervised learning, supervised learning techniques are regarded as \n",
"less complex and more \"trustworthy\".\n",
"\n",
"Here are some examples:\n",
"\n",
"- Supervised learning: Support vector machine, Neural network, Linear and logistics regression, \n",
"random forest, and Classification trees.\n",
"\n",
"- Unsupervised learning: K-means, Hierarchical clustering, Principal Component Analysis (PCA)\n",
"\n",
"In the case of the earnings calls dataset that we formed by parsing motley fool, \n",
"we can *label* each call according to the realized returns around the time of the \n",
"earnings call. We can then use these returns as a proxy that indicates the overall \n",
"\"sentiment\" of each call. The literature refers to these returns as the Earnings Announcement \n",
"Return, or EAR. From EAR, we can proceed to define or label each earnings calls as \n",
"being either \"Positive\" or \"Negative\". The EAR any given firm (stock) is simply its \n",
"abnormal return over a three day window ***centered*** on the earnings announcement.\n",
"\n",
"## Import packages and load scripts\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 1, "outputs": [], 
           "source": ["open System\n",
"Environment.CurrentDirectory \u003c- __SOURCE_DIRECTORY__\n",
"\n",
"#load \"Types.fsx\"\n",
"#load \"Common.fsx\"\n",
"#r \"nuget: FSharp.Data\"\n",
"#r \"nuget: Plotly.NET, 2.0.0-preview.6\"\n",
"\n",
"if false then\n",
"    let tiingoKey = System.Environment.GetEnvironmentVariable \"TIINGO_API_KEY\"\n",
"    ()\n",
"\n",
"open Types\n",
"open Common\n",
"open Common.Tiingo\n",
"\n",
"open Newtonsoft.Json\n",
"open Plotly.NET\n",
"\n",
"fsi.AddPrinter\u003cDateTime\u003e(fun dt -\u003e dt.ToString(\"s\"))\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Reading Transcript data from .json file:\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 2, "outputs": [], 
           "source": ["/// JSON data reader\n",
"let readEarningsCallJson (jsonFile: string) =\n",
"    IO.File.ReadAllText(jsonFile)\n",
"    |\u003e fun json -\u003e JsonConvert.DeserializeObject\u003carray\u003cEarningsCall\u003e\u003e(json)\n",
"\n",
"/// Calls data\n",
"let myCalls = \n",
"    [|\n",
"    \"data-cache/EarningsCall2018.json\"\n",
"    \"data-cache/EarningsCall2019.json\"\n",
"    \"data-cache/EarningsCall2020.json\"\n",
"    \"data-cache/EarningsCall2021.json\"\n",
"    |]\n",
"    |\u003e Array.collect readEarningsCallJson\n",
"    |\u003e Array.sortBy (fun xs -\u003e xs.CallId.Date)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## Barplots: Timing of calls\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 3, "outputs": [], 
           "source": ["let callsByTimeOfDay (calls : EarningsCall []) = \n",
"    calls\n",
"    |\u003e Array.countBy (fun xs -\u003e xs.CallId.Date.Hour)\n",
"    |\u003e Array.sortBy (fun (hour, _) -\u003e hour)\n",
"    |\u003e Chart.Column\n",
"    |\u003e Chart.withTitle $\"Earnings Calls by time of day (N: {Seq.length calls})\"\n",
"    |\u003e Chart.withX_AxisStyle \"Hour\"\n",
"    |\u003e Chart.withY_AxisStyle \"Count\"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": null, "outputs": [
          {
           "data": {
            "text/plain": ["No value returned by any evaluator"]
        },
           "execution_count": null,
           "metadata": {},
           "output_type": "execute_result"
          }], 
           "source": ["myCalls |\u003e callsByTimeOfDay |\u003e Chart.Show\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## Tiingo returns\n",
"\n",
"Provided that Tiingo supports the ticker we are looking for, we can use \n",
"the `Tiingo` module from `Common.fsx` to download ticker related data such\n",
"as closing price and volume. Since we might be interested in analyzing the \n",
"stock\u0027s movement following the earnings call we\u0027ll fetch data up until 60 days \n",
"after each call. We can use this same 60 day window of ticker observations to\n",
"compute the EAR for each call.\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 5, "outputs": [], 
           "source": ["/// Tiingo data\n",
"let tiingoWindow (tiingoStart: DateTime)\n",
"                 (tiingoEnd: DateTime)\n",
"                 (ticker: string) =\n",
"    let checkObs obs = \n",
"        match obs with\n",
"        | [||] -\u003e None\n",
"        | _ -\u003e Some obs\n",
"        \n",
"    ticker\n",
"    |\u003e Tiingo.request\n",
"    |\u003e Tiingo.startOn tiingoStart\n",
"    |\u003e Tiingo.endOn tiingoEnd\n",
"    |\u003e Tiingo.get\n",
"    |\u003e checkObs\n",
"\n",
"let calcReturn pv fv = \n",
"    (fv / pv) - 1.0\n",
"\n",
"let getReturnObs (ticker: string) (obs: TiingoObs []) = \n",
"    obs\n",
"    |\u003e Seq.pairwise\n",
"    |\u003e Seq.map (fun (yesterday, today) -\u003e \n",
"        { Symbol = ticker\n",
"          Date = today.Date\n",
"          Return = calcReturn (float yesterday.AdjClose) (float today.AdjClose) })\n",
"    |\u003e Seq.toArray\n",
"\n",
"let earBarPlot (ticker: string) = \n",
"    myCalls \n",
"    // Find first matching observation\n",
"    |\u003e Seq.tryFind (fun xs -\u003e xs.CallId.Ticker = ticker) \n",
"    |\u003e Option.bind (fun call -\u003e \n",
"        tiingoWindow (call.CallId.Date.AddDays(-7.)) (call.CallId.Date.AddDays(7.)) ticker\n",
"        |\u003e fun xs -\u003e\n",
"            match xs with\n",
"            | Some obs -\u003e \n",
"                getReturnObs ticker obs\n",
"                // Plot\n",
"                |\u003e Array.map (fun xs -\u003e xs.Date, xs.Return)\n",
"                |\u003e Chart.Bar\n",
"                |\u003e Chart.withTitle \n",
"                    $\"{ticker} Earnings Call {call.CallId.Date} Q{call.CallId.FiscalQuarter.ToString()}\"\n",
"                |\u003e Some\n",
"            | None -\u003e None)\n",
"\n",
"let msftEarPlot = earBarPlot \"MSFT\"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": null, "outputs": [
          {
           "data": {
            "text/plain": ["No value returned by any evaluator"]
        },
           "execution_count": null,
           "metadata": {},
           "output_type": "execute_result"
          }], 
           "source": ["msftEarPlot |\u003e Option.map Chart.Show\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Earnings Announcement Return\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 7, "outputs": [], 
           "source": ["/// Sample range\n",
"let startSample, endSample =    \n",
"    myCalls\n",
"    |\u003e Seq.map (fun xs -\u003e xs.CallId.Date)\n",
"    |\u003e fun dates -\u003e \n",
"        (Seq.min dates).AddDays(-10.), (Seq.max dates).AddDays(10.)\n",
"\n",
"/// SP500 (SPY)\n",
"let spyObs = \n",
"    let spyTicker = \"SPY\"\n",
"\n",
"    let getReturnsMap (tiingoObs: Tiingo.TiingoObs []) =\n",
"        getReturnObs spyTicker tiingoObs\n",
"        |\u003e Array.map (fun xs -\u003e xs.Date, xs)\n",
"        |\u003e Map\n",
"\n",
"    let checkSpy rets =\n",
"        match rets with \n",
"        | None -\u003e failwith \"why isn\u0027t Tiingo working\"\n",
"        | Some rets -\u003e rets\n",
"        \n",
"    spyTicker\n",
"    |\u003e tiingoWindow startSample endSample\n",
"    |\u003e checkSpy\n",
"    |\u003e getReturnsMap\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["## Earnings Announcement Return\n",
"\n",
"### Tiingo observation window\n",
"\n",
"Sometimes a call might happen on a friday or right before or after a long holiday. \n",
"In these particular case scenarios, we have to be extra careful when trying to find \n",
"our three-day return window.\n",
"\n",
"Because we don\u0027t have a database with all the non-trading days of a given year, \n",
"instead of trying to match a three-day return window instantaneously, it is safer \n",
"if we work from a range of return observations and try to find our three-day return \n",
"window from there. \n",
"\n",
"#### Three-day windows\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 8, "outputs": [], 
           "source": ["/// Three day return window\n",
"let findThreeDays (middleObs: ReturnObs) (rets: ReturnObs []): ReturnObs [] option = \n",
"    rets\n",
"    |\u003e Seq.windowed 3\n",
"    |\u003e Seq.tryFind (fun retWindow -\u003e\n",
"        let middle = retWindow.[1]\n",
"        middle.Date.Date = middleObs.Date.Date)\n",
"\n",
"/// SPY returns window\n",
"let spyReturnsBetween (begWin: DateTime) (endWin: DateTime) =\n",
"    let rec loop (date: DateTime) rets =\n",
"        if date.Date \u003c= endWin.Date then\n",
"            match Map.tryFind date spyObs with\n",
"            | Some spy -\u003e loop (date.AddDays(1.0)) (spy::rets)\n",
"            | None -\u003e loop (date.AddDays(1.0)) rets\n",
"        else rets\n",
"    loop begWin []\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["#### Adjusted returns\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 9, "outputs": [], 
           "source": ["/// Abnormal returns from three day window\n",
"let computeAdjReturns (stock : ReturnObs []) = \n",
"    let begWin, endWin = \n",
"        stock\n",
"        |\u003e Seq.sortBy (fun xs -\u003e xs.Date)\n",
"        |\u003e Seq.map (fun xs -\u003e xs.Date)\n",
"        |\u003e fun xs -\u003e \n",
"            (xs |\u003e Seq.head), (xs |\u003e Seq.last)\n",
"    \n",
"    let cumRet rets =\n",
"        (1.0, rets)\n",
"        ||\u003e Seq.fold(fun acc ret -\u003e acc*(1.0+ret)) \n",
"\n",
"    let spy = \n",
"        spyReturnsBetween begWin endWin\n",
"        |\u003e Seq.map (fun xs -\u003e xs.Return)\n",
"        |\u003e cumRet\n",
"        \n",
"    let stockRet = \n",
"        stock \n",
"        |\u003e Seq.map(fun x -\u003e x.Return) \n",
"        |\u003e cumRet\n",
"\n",
"    stockRet - spy\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["#### EAR\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 10, "outputs": [], 
           "source": ["type Sentiment = \n",
"    | Positive\n",
"    | Negative\n",
"    | Neutral\n",
"\n",
"type EarningsAnnouncementReturn =\n",
"    { EarningsCall: EarningsCall\n",
"      TiingoObs: Tiingo.TiingoObs []\n",
"      Sentiment: Sentiment option \n",
"      Ear: float option }\n",
"\n",
"/// Find first observed return\n",
"let firstReturnAfterCall (call: EarningsCall) (returnObs: ReturnObs []) = \n",
"    let date = call.CallId.Date\n",
"    if date.Hour \u003c 16 then date.Date \n",
"    else date.Date.AddDays(1.0)\n",
"    \n",
"    |\u003e fun dateOfCall -\u003e \n",
"        returnObs\n",
"        |\u003e Seq.tryFind (fun xs -\u003e xs.Date.Date \u003e= dateOfCall.Date)\n",
"\n",
"let computeEar (call: EarningsCall) (tiingoObs: Tiingo.TiingoObs []) = \n",
"\n",
"    let getAdjReturns middleObs returnObs = \n",
"        match findThreeDays middleObs returnObs with\n",
"        | Some threeDayWindow -\u003e Some (computeAdjReturns threeDayWindow)\n",
"        | None -\u003e None\n",
"    \n",
"    getReturnObs call.CallId.Ticker tiingoObs\n",
"    |\u003e fun retObs -\u003e\n",
"        firstReturnAfterCall call retObs\n",
"        |\u003e Option.bind (fun middleObs -\u003e \n",
"            getAdjReturns middleObs retObs)\n",
"\n",
"let generateEar (call: EarningsCall) = \n",
"    let tiingoWindow = \n",
"        let flatDate = call.CallId.Date.Date\n",
"        tiingoWindow (flatDate.AddDays(-10.0)) (flatDate.AddDays(70.0)) call.CallId.Ticker\n",
"\n",
"    match tiingoWindow with\n",
"    | Some tiingoObs -\u003e \n",
"        // For now lets set Sentiment to None\n",
"        Some { EarningsCall = call\n",
"               TiingoObs = tiingoObs\n",
"               Sentiment = None\n",
"               Ear = computeEar call tiingoObs}\n",
"    | None -\u003e None\n",
"\n",
"let tslaCall = \n",
"    myCalls\n",
"    |\u003e Array.tryFind (fun xs -\u003e xs.CallId.Ticker = \"TSLA\")\n",
"    |\u003e Option.bind generateEar\n",
"\n",
"tslaCall |\u003e Option.bind (fun xs -\u003e xs.Ear)\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Async methods\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 11, "outputs": [], 
           "source": ["module Async =\n",
"    let ParallelThrottled xs = Async.Parallel(xs, 100)\n",
"\n",
"let asyncCall (call: EarningsCall) =\n",
"    let rec loop attempt n =\n",
"        async {\n",
"            try\n",
"                return generateEar call\n",
"            with e -\u003e\n",
"                if attempt \u003e 0 then\n",
"                    do! Async.Sleep 2000 // Wait 2 seconds in case we\u0027re throttled.\n",
"                    return! loop (attempt - 1) n\n",
"                else return! failwithf \"Failed to request \u0027%s\u0027. Error: %O\" call.CallId.Ticker e }\n",
"    loop 10 call \n",
"\n",
"let asyncCalls (calls: EarningsCall []) = \n",
"    calls\n",
"    |\u003e Seq.map asyncCall\n",
"    |\u003e Async.ParallelThrottled\n",
"    |\u003e Async.RunSynchronously\n",
"    |\u003e Array.choose id\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": null, "outputs": [], 
           "source": ["let getEarsByYear year = \n",
"    myCalls\n",
"    |\u003e Array.filter (fun xs -\u003e xs.CallId.Date.Year = year)\n",
"    |\u003e asyncCalls\n",
"\n",
"let ears2018 = getEarsByYear 2018\n",
"let ears2019 = getEarsByYear 2019\n",
"let ears2020 = getEarsByYear 2020\n",
"let ears2021 = getEarsByYear 2021\n",
"\n",
"let calls2018, calls2019, calls2020, calls2021 = \n",
"    myCalls\n",
"    |\u003e fun xs -\u003e \n",
"        (xs |\u003e Array.filter (fun xs -\u003e xs.CallId.Date.Year = 2018)),\n",
"        (xs |\u003e Array.filter (fun xs -\u003e xs.CallId.Date.Year = 2019)),\n",
"        (xs |\u003e Array.filter (fun xs -\u003e xs.CallId.Date.Year = 2020)),\n",
"        (xs |\u003e Array.filter (fun xs -\u003e xs.CallId.Date.Year = 2021))\n",
"\n",
"calls2018.Length\n",
"ears2018.Length\n",
"\n",
"calls2019.Length\n",
"ears2019.Length\n",
"\n",
"calls2020.Length\n",
"ears2020.Length\n",
"\n",
"calls2021.Length\n",
"ears2021.Length\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["### Download and Export to Json\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": null, "outputs": [], 
           "source": ["let earToJson (fileName: string) (ears: EarningsAnnouncementReturn [])  = \n",
"    JsonConvert.SerializeObject(ears)\n",
"    |\u003e fun json -\u003e IO.File.WriteAllText(fileName, json)\n",
"\n",
"earToJson \"data-cache/EarningsAnnouncementReturn2018.json\" ears2018\n",
"earToJson \"data-cache/EarningsAnnouncementReturn2019.json\" ears2019\n",
"earToJson \"data-cache/EarningsAnnouncementReturn2020.json\" ears2020\n",
"earToJson \"data-cache/EarningsAnnouncementReturn2021.json\" ears2021\n"]
          }],
            "metadata": {
            "kernelspec": {"display_name": ".NET (F#)", "language": "F#", "name": ".net-fsharp"},
            "langauge_info": {
        "file_extension": ".fs",
        "mimetype": "text/x-fsharp",
        "name": "C#",
        "pygments_lexer": "fsharp",
        "version": "4.5"
        }
        },
            "nbformat": 4,
            "nbformat_minor": 1
        }
        

